terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.16"
    }
  }

  required_version = ">= 1.2.0"
}

provider "aws" {
  region = var.region
}

# Get current AWS Account ID
data "aws_caller_identity" "current" {}

# Load user_data from local file
data "template_file" "user_data" {
  template = file("${path.module}/bash_scripts/user_data.sh")

  vars = {
    account_id = data.aws_caller_identity.current.account_id
    region     = var.region
  }
}

# IAM Role for EC2 to access ECR
resource "aws_iam_role" "ec2_role" {
  name = "ec2-ecr-access-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17",
    Statement = [{
      Action = "sts:AssumeRole",
      Effect = "Allow",
      Principal = {
        Service = "ec2.amazonaws.com"
      }
    }]
  })
}

# Attach AmazonEC2ContainerRegistryReadOnly policy
resource "aws_iam_role_policy_attachment" "ecr_access" {
  role       = aws_iam_role.ec2_role.name
  policy_arn = "arn:aws:iam::aws:policy/AmazonEC2ContainerRegistryReadOnly"
}

# Instance profile for EC2
resource "aws_iam_instance_profile" "ec2_instance_profile" {
  name = "ec2-ecr-access-profile"
  role = aws_iam_role.ec2_role.name
}

# Security group
resource "aws_security_group" "app_sg" {
  name        = "app-server-sg"
  description = "Allow SSH and HTTP access"
  # vpc_id      = will be added later

  ingress {
    description = "SSH"
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    description = "HTTP"
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    description = "All traffic out"
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# EC2 instance
resource "aws_instance" "app_server" {
  ami                    = var.machine_image
  instance_type          = var.instance_type
  key_name               = var.key_pair_name_for_ssh
  iam_instance_profile   = aws_iam_instance_profile.ec2_instance_profile.name
  user_data              = data.template_file.user_data.rendered
  vpc_security_group_ids = [aws_security_group.app_sg.id]

  tags = {
    Name = var.instance_name
  }
}